# Maintainer: Filipe La√≠ns (FFY00) <lains@archlinux.org>

pkgname=arduino-cli
pkgver=0.18.1
pkgrel=1
arch=('x86_64')
pkgdesc='Arduino command line interface'
url='https://github.com/arduino/arduino-cli'
license=('GPL3')
groups=('arduino')
makedepends=('go' 'git')
optdepends=('arduino-avr-core: upstream avr-gcc and avrdude')
source=("git+$url.git#tag=$pkgver"
        "$pkgname.sh"
        "change-test-dirs.patch")
sha512sums=('SKIP'
            'SKIP'
            'SKIP')

build(){
  cd $pkgname
  
  patch -p1 < ../change-test-dirs.patch
  export CGO_CPPFLAGS="$CPPFLAGS"
  export CGO_CFLAGS="$CFLAGS"
  export CGO_CXXFLAGS="$CXXFLAGS"
  export CGO_LDFLAGS="$LDFLAGS"
  export GOFLAGS='-buildmode=pie -trimpath -mod=readonly -modcacherw'

  go build \
        -ldflags "-linkmode=external -X github.com/arduino/arduino-cli/version.versionString=$pkgver-arch -X github.com/arduino/arduino-cli/version.commit=$(git rev-parse HEAD)" \
        -v .
}

package() {
  $pkgname/$pkgname completion bash > $pkgname.bash
  $pkgname/$pkgname completion zsh  > $pkgname.zsh
  
  install -Dm 755 $pkgname/$pkgname       "$pkgdir"/usr/share/arduino/$pkgname
  # symlinked binary cant find local platform, using script instead
  install -Dm 755 $srcdir/$pkgname.sh     "$pkgdir"/usr/bin/$pkgname
  install -Dm 644 $pkgname.bash           "$pkgdir"/usr/share/bash-completion/completions/$pkgname
  install -Dm 644 $pkgname.zsh            "$pkgdir"/usr/share/zsh/site-functions/_$pkgname
  install -Dm 644 $pkgname/LICENSE.txt    "$pkgdir"/usr/share/licenses/$pkgname/GPL-3.0
}

